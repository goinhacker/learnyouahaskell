# 사용자 정의 타입 만들기4

## 타입클래스

지난 챕터에서 몇가지 하스켈의 표준 타입클래스들을 배우고, 어떤 타입들이 속하는지 보았습니다. 또한 사용자 정의 타입을 표준 타입클래스로부터 파생시키는 방법을 살펴보았습니다. 이번 섹션에서는 사용자 정의 타입클래스를 만들고, 그것으로부터 파생되는 타입을 만들어 보겠습니다.

그동안 배운 타입클래스에 대한 내용을 요약하면 타입클래스는 인터페이스와 같다는 것입니다. 타입클래스는 동일한지 비교하거나, 순서를 비교하는 등의 몇가지 동작을 정의합니다. 그리고 타입은 타입클래스의 인스턴스로 만듬으로써 해당 동작을 할 수 있습니다. 타입클래스들의 동작은 함수들을 정의하거나 구현한 선언을 입력하여 얻을 수 있습니다. 어떤 타입이 타입클래스의 인스턴스라는 것은 타입이 타입클래스의 동작을 할 수 있다는 것을 의미합니다.

타입클래스는 자바나 파이썬의 클래스와는 전혀 관계가 없습니다. 이 부분이 헷갈리수 있으므로 지금부터는 명령형 언어의 클래스들에 대해서는 완전히 잊어야 합니다!!

예를들어, `Eq` 타입클래스는 동일한지를 확인할 수 있는데 `==`과 `/=` 함수를 정의하고 있습니다. 만약 `Car`라는 타입이 `Eq` 타입클래스의 인스턴스라면, 두개의 차가 동일한지 여부를 `==`함수로 확인할 수 있습니다. 

`Eq` 타입클래스는 표준 prelude 모듈에 아래와 같이 정의되어 있습니다. 

```haskell
class Eq a where
    (==) :: a -> a -> Bool
    (/=) :: a -> a -> Bool
    x == y = not (x /= y)
    x /= y = not (x == y)
```  

여기서 `class Eq a where`는 `Eq`라는 새로운 타입클래스를 정의하는 것을 의미합니다. 여기서 `a`는 타입 변수이고, `Eq`의 인스턴스가 될 타입의 역할을 수행합니다. `a` 다른 이름으로 쓰일 수 있고, 한개의 문자가 아니어도 되지만 소문자로 구성되어야 합니다. 그 다음에는 여러가지 함수들이 정의되었습니다. 여기서 함수의 바디를 구현하는 것은 필수가 아닙니다. 따라서 그냥 함수의 선언만 명시해도 됩니다. 

어쨋든 여기는 `Eq`가 정의한 함수에 대해서 함수의 바디를 구현했지만, 상호 재귀 관점에서만 정의했습니다. `Eq`의 인스턴스 두개가 다르지 않다면 같은 것이고, 같지않다면 다를 것입니다. 이런 구현은 반드시 하지않아도 되지만 어떤 도움을 줄 수 있는지 보게 될 것 입니다. 

```haskell
data TrafficLight = Red | Yellow | Green
```

TrafficLight는 신호등의 상태를 정의합니다. TrafficLight는 클래스 인스턴스를 파생하지 않았습니다. `Eq`나 `Show`로 파생시킬수도 있지만, 여기서는 직접 어떤 인스턴스를 작성해보도록 하겠습니다. 

```haskell
instance Eq TrafficLight where
    Red == Red = True
    Green == Green = True
    Yellow == Yellow = True
    _ == _ = False
``` 

_instance_ 키워드를 사용해서 인스턴스로 작성하였습니다. _class_는 새로운 타입클래스를 정의하고, _instance_는 타입클래스의 타입 인스턴스를 만듭니다. `Eq`를 정의할때 `class Eq a where`로 작성하고 여기서 `a`는 나중에 인스턴스가 만들어질때 타입의 역할을 한다고 했습니다. 여기서는 `instance Eq TrafficLight where`로 작성하여 바로 인스턴스로 만들었기 때문에 좀 더 명확합니다. `a`를 실제 타입으로 변경하였습니다. 

_class_ 선언안에서 `==`은 `\=`와 그 반대의 관점에서 정의되었기 때문에, 인스턴스 선언안에 둘 중 하나를 겹쳐서 사용했습니다. 이것을 타입클래스의 **최소한의 완전한 선언**이라고 부릅니다(타입이 클래스처럼 동작할 수 있도록 하기위해 구현해야하는 최소한의 함수). `Eq`가 최소한의 완전한 선언을 수행하려면 `==` 또는 `\=`중 하나를 overwrite해야 합니다. `Eq`를 간단히 정의하면 아래와 같습니다.   

```haskell
class Eq a where
    (==) :: a -> a -> Bool
    (\=) :: a -> a -> Bool
```

이렇게 선언된 타입클래스의 인스턴스 타입을 만들때는 두 함수 모두 구현해야 합니다. 왜냐하면 하스켈은 두 함수가 어떻게 관련되어 있는지 알지못하기 때문입니다. 최소한의 완전한 선언은 `==`과 `\=` 모두가 될 것입니다.

이번에는 TrafficLight를 `Show`의 인스턴스로 만들어 보겠습니다. `Show`에 대한 최소한의 완전한 정의를 만족하기 위해서, 값을 문자열로 변환하는 `show` 함수를 구현해야 합니다. 

```haskell
instance Show TrafficLight where
    show Red = "Red light"
    show Yellow = "Yellow light"
    show Green = "Green light"
```

![](/assets/스크린샷 2017-07-04 오전 12.22.12.png)

간단하게 `Eq`는 파생시키는 방법으로도 동일한 효과를 얻을수 있습니다.(여기서는 교육을 목적으로 직접 작성) 하지만 `Show`의 경우는 그냥 파생시키면 직접 문자열을 `"Red light"`와 같이 지정하여 출력할 수 없습니다. 이 경우는 반드시 _instance_ 선언으로 직접 작성해야 합니다. 

또한 타입클래스의 서브클래스인 타입클래스를 만들수도 있습니다. `Num` 타입클래스에 대한 _class_ 선언은 매우 길지만, 첫줄은 아래와 같습니다. 

```haskell
class (Eq a) => Num a where
  ...
```   

이전에도 언급했듯이 클래스 제약안에는 여러개를 넣을 수 있습니다. 따라서 위의 선언은 `class Num a where`에서 `a`가 `Eq`의 인스턴스여야 한다는 제약만 추가한 것입니다. 이 선언을 통해서 숫자를 고려한 어떤 타입을 고려하기전에, 이 타입의 값이 같은지 여부를 판단할 수 있는 값인지 확인해야 합니다. 단지 _class_ 선언에 대한 클래스 제약을 만드는 것이 전부입니다. _class_나 _instance_ 선언안에 함수의 바디를 정의할때, `a`의 타입은 `Eq`의 인스턴스이고 `==`를 사용할 수 있다는 것을 가정할 수 있습니다.   

`Maybe`나 리스트 타입은 어떻게 타입클래스의 인스턴스로 만들 수 있을까요? 여기서 `Maybe`와 `TrafficLight`의 차이점은 `Maybe`는 그 자체적으로 구체적인 타입(concrete type)이 아니고 한개의 파라메터를 받아서 구체적인 타입을 만드는 타입 생성자(type constructor)라는 점입니다.(예를들면, `Maybe Char` 처럼..)

여기서 다시한번 `Eq` 타입클래스를 살펴보겠습니다. 

```haskell
class Eq a where
    (==) :: a -> a -> Bool
    (/=) :: a -> a -> Bool
    x == y = not (x /= y)
    x /= y = not (x == y)
```

이 타입 선언에서 `a`는 함수안의 모든 타입들은 이미 구체화(concrete)되어 있어야 하기때문에, 구체적인 타입(concrete type)으로 사용된다는 것을 알 수 있습니다(예를들면, 함수의 타입이 `a -> Maybe`일수는 없지만, `a -> Maybe a`나 `Maybe Int -> Maybe String`일수는 있습니다.). 따라서 아래와 같이 선언할수는 없습니다. 

```haskell
instance Eq Maybe where
   ...
```

왜냐하면 `a`는 구체적인 타입이어야 하는데 `Maybe`는 타입 생성자이기 때문입니다. 이것을 모든 타입에 대해서 `instance Eq (Maybe Int) where`, `instance Eq (Maybe Char) where`와 같이 작성하는 것은 매우 번거로운 일입니다. 그래서 아래와 같이 작성합니다. 

```haskell
instance Eq (Maybe m) where
    Just x == Just y = x == y
    Nothing == Nothing = True
    _ == _ = False
```

이렇게 작성하면 모든 타입에 대해서 `Maybe something` 형태의 `Eq` 인스턴스를 만들 수 있습니다. `claa Eq a wherer`의 `a`와 마찬가지로 `(Maybe m)`은 구제적인 타입입니다(여기서 `m`은 소문자여야함). `(Maybe m)`은 `m`이 `Eq`의 인스턴스인 모든 타입이 될 수 있습니다. 

여기에는 한가지 문제가 있습니다. `Maybe` 자체는 `==`을 사용할 수 있다는 것은 알지만, `Maybe`가 가지고 있는 `m`이 `Eq`의 인스턴스인지는 알 수 없습니다. 따라서 아래와 같이 _instance_ 선언을 수정해야 합니다. 

```haskell
instance (Eq m) => Eq (Maybe m) where
    Just x == Just y = x == y
    Nothing == Nothing = True
    _ == _ = False
```    

여기서는 _instance_ 선언에 클래스 한정을 추가해 주었습니다. 이렇게하면 `Maybe m`뿐만 아니라 `m`도 `Eq`의 인스턴스 입니다. 이것은 실제로 하스켈이 인스턴스를 파생시키는 방법입니다. 

대부분의 경우 _class_ 선언내에서 클래스 한정은 타입클래스의 서브클래스인 타입클래스를 만들기 위해서 사용됩니다. 그리고 _instance_ 선언에서는 어떤 타입의 내용물(contents)에 대한 요구사항을 표현하기 위해서 사용됩니다. 예를들어, `Maybe`에 포함된 내용물(contents)도 `Eq` 타입클래스의 인스턴스라는 것입니다. 

만약 인스턴스를 만들때 타입 선언안에서 타입이 구체적인 타입으로 사용된다면(`a -> a -> Bool`에서 `a`처럼), 타입 파라메터들을 제공하고 괄호를 추가하여 구체적인 타입으로 끝나야 합니다. 

인스턴스를 만들려는 타입이 _class_ 선언의 매개변수를 대체한다는 점을 고려해야 합니다. `class Eq a where`의 `a`는 인스턴스로 만들어질때 실제 타입으로 대체될 것 입니다. 따라서 타입을 함수 타입 선언안에도 넣도록 해야합니다. `(==) :: Maybe -> Maybe -> Bool`는 별로 의미가 없지만 `(==) :: (Eq m) => Maybe m -> Maybe m -> Bool`는 그렇지 않습니다. 이렇게되면 어떤 경우에 관계없이 `==`가 항상 `(==) :: (Eq a) => a -> a -> Bool` 타입을 갖기 때문에 고민해봐야할 문제입니다. 

만약 타입클래스의 인스턴스가 무엇인지 알려면, GHCI에서 `:info YourTypeClass`라고 치면 됩니다. 따라서 `:info Num`이라고 치면 타입클래스가 정의하는 함수와 타입클래스에 있는 타입 목록을 보여줍니다. 또한 `:info`는 타입과 타입 생성자에 대해서도 동작합니다. 만약 `:info Maybe`라고 하면, `Maybe`가 인스턴스인 모든 타입클래스들을 보여줄 것 입니다. 또한 `info`는 함수의 타입 선언을 보여줄 수도 있습니다. 

## yes-no 타입클래스 예제

자바스크립트와 같은 약한 타입 언어에서는 if 표현식안에 거의 모든 것을 넣을 수 있습니다. 예를들어, `if (0) alert("YEAH!") else alert("NO!")`, `if ("") alert("YEAH!") else alert("NO!")`, `if (false) alert("YEAH!") else alert("NO!")` 등의 같은 표현이 가능합니다. 그리고 결과는 모두 `"NO!"`를 출력합니다. 만약 `if ("WHAT") alert("YEAH!") else alert("NO!")`는 `"YEAH!"`를 출력합니다. 왜냐하면 자바스크립트에서 비어져있지 않은 문자열은 true이기 때문입니다. 

하스켈에서는 boolean을 엄격하게 `Bool`로 사용하는 것이 좋지만, 여기서는 자바스크립트처럼 동작하도록 구현해 보겠습니다. 먼저 _class_ 선언으로 시작합니다.  

```haskell
class YesNo a where
    yesno :: a -> Bool
```

`YesNo` 타입클래스는 하나의 함수로 정의되었습니다. 이 함수는 참과 거짓의 의미를 가질 수 있는 타입의 값을 받아서 참인지 여부를 알려줍니다. 여기서 `a`는 구체적인 타입이 되어야 합니다. 

다음으로 숫자에 대한 인스턴스를 정의하겠습니다. 여기서는 자바스크립트처럼 0이면 거짓, 1이면 참으로 하겠습니다. 

```haskell
instance YesNo Int where
    yesno 0 = False
    yesno _ = True
```    

리스트에 대한 인스턴스는 빈리스트면 거짓이고, 그렇지 않으면 참입니다.(문자열도 리스트로 정의됩니다.) 

```haskell
instance YesNo [a] where
    yesno [] = False
    yesno _ = True
```

`Bool`은 이미 참과 거짓의 분명하기 때문에 아래와 같이 정의합니다. 

```haskell
instance YesNo Bool where
    yesno = id
```

여기서 `id`는 어떤 파라메터를 받아서 동일한 것을 리턴해주는 표준 라이브러리 함수 입니다.

`Maybe a`에 대한 인스턴스도 정의하겠습니다. 

```haskell
instance YesNo (Maybe a) where
    yesno (Just _) = True
    yesno Nothing = False
```

여기서도 `Maybe`의 내용물로 어떤 가정도 만들지 않기 때문에 클래스 한장자는 불필요합니다. 그냥 `Nothing`이면 거짓을 `Just`이면 참이 됩니다. `Maybe`만으로는 구체적인 타입이 될 수 없기때문에(`Maybe -> Bool`이라는 함수가 존재할 수 없듯이) `(Maybe a)`를 넣어주어야 합니다(`Maybe a -> Bool`은 가능하므로). 이렇게하면 `Maybe something`의 `something`이 무엇이든지 `YesNo`의 일부가 됩니다. 

이전 챕터에서 정의한 `Tree a` 타입도 만들 수 있습니다. 비어있는 트리는 거짓이고 그렇지 않으면 참입니다. 

```haskell
instance YesNo (Tree a) where
    yesno EmptyTree = False
    yesno _ = True
```

TrafficLight는 빨간불인 경우에만 거짓으로 하고, 아래와 같이 정의하겠습니다. 

```haskell
instance YesNo TrafficLight where
    yesno Red = False
    yesno _ = True
```

이제 정의한 인스턴스들을 확인해보면 아래와 같습니다. 

![](/assets/스크린샷 2017-07-05 오후 11.55.31.png)

이제 `YesNo`의 값들로 동작하는 가짜 if문을 만들어보겠습니다. 

```haskell
yesnoIf :: (YesNo y) => y -> a -> a -> a
yesnoIf yesnoVal yesResult noResult = if yesno yesnoVal then yesResult else noResult
```

`yesnoIf` 함수는 참거짓을 판단할 값과, 참일때 리턴할 값과 거짓일때 리턴할 값을 받습니다. 실제로 실행해보면 아래와 같습니다. 

![](/assets/스크린샷 2017-07-06 오전 12.01.11.png)













 



    

  

